group = 'com.tweissbeck.gradle.plugin.sdkmanager'
version = '1.0.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
    acceptanceTest { sourceSet ->
        sourceSet.java.srcDir file('src/acceptanceTest/java')
        sourceSet.groovy.srcDir file('src/acceptanceTest/groovy')
    }
}

task acceptanceTest(type: Test) {
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
}

acceptanceTest.mustRunAfter project.tasks.findByName('test')
acceptanceTest.description = 'Runs the acceptance tests.'
acceptanceTest.group = JavaBasePlugin.VERIFICATION_GROUP

check.dependsOn acceptanceTest

/* Maven publish configuration */
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId 'gradle-plugin'
            /** Modify pom */
            pom.withXml {
                asNode().appendNode('description',
                        'Gradle plugin which downloads and manages your Android SDK.')
                Node scm = asNode().appendNode('scm')
                scm.appendNode('url', 'https://github.com/tweissbeck/sdk-manager-plugin')
                scm.appendNode('connection', 'scm:git:git://github.com/tweissbeck/sdk-manager-plugin.git')
                scm.appendNode('developerConnection', 'scm:git:ssh://git@github.com/tweissbeck/sdk-manager-plugin.git')

                Node licenses = asNode().appendNode('licenses')
                Node license = licenses.appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')

                Node developers = asNode().appendNode('developers')
                def appendDev = { Node parent, String id, String name, String email ->
                    Node developer = new Node(parent, 'developer')
                    developer.appendNode('id', id)
                    developer.appendNode('name', name)
                    developer.appendNode('email', email)
                    return developer
                }

                appendDev(developers, 'jakewharton', 'Jake Wharton', 'jakewharton@gmail.com')
                appendDev(developers, 'tweissbeck', 'Thierry Weissbeck', 'thierry.weissbeck+github@gmail.com')

            }

            /** This adds groovy files too (groovy plugin) */
            from components.java
        }
    }
}

dependencies {
    repositories {
        mavenLocal()
        mavenCentral()
    }

    compile gradleApi()
    compile localGroovy()
    compile 'com.android.tools.build:gradle:1.5.0'
    compile 'org.rauschig:jarchivelib:0.6.0'
    compile 'commons-io:commons-io:2.4'

    testCompile 'org.easytesting:fest-assert-core:2.0M10'

    acceptanceTestCompile sourceSets.main.output
    acceptanceTestCompile sourceSets.test.output
    acceptanceTestCompile configurations.testCompile
    acceptanceTestRuntime configurations.testRuntime
}

